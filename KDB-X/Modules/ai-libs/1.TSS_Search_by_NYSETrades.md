# Temporal Similarity Search (TSS) on NYSE trades

TSS (Temporal Similarity Search) is a technique used to find patterns in time series data by comparing a query pattern to a longer series. It’s useful when you want to identify where a particular shape or trend (like a spike, dip, or repeated structure) occurs in your data — such as finding similar price movements, temperature trends, or signal patterns.

This tutorial walks through the process of creating database filled with a day's worth of NYSE data. It then demonstrates how to conduct TSS efficiently using by clauses to pattern search across symbols.

## 1. Prerequisites

1. Requires KDB-X to be installed, you can sign up at https://kdb-x.kx.com/sign-in.
2. Ensure you have the necessary dataset:
   1. Download the NYSE sample dataset from NYSE: [US Trades](https://ftp.nyse.com/Historical%20Data%20Samples/TAQ%20NYSE%20TRADES/EQY_US_TAQ_NYSE_TRADES_20231002.gz)
   2. Copy into the same directory where you are running this from

## 2. Loading and Preparing the Data

Launch a q session using ```q -s 4``` to take advantage of parallel execution, and load the ai-libs initialization script:
```q
\l ai-libs/init.q
```

Load the dataset (`EQY_US_TAQ_NYSE_TRADES_20231002.gz`) into a table:

```q
file:"EQY_US_TAQ_NYSE_TRADES_20231002.gz";
system"rm -f fifo && mkfifo fifo";
trade:([] msgType:();sequenceNo:();time:();sym:();tradeId:();price:();volume:());
system"gunzip -cf ",file," > fifo &";
.Q.fps[{`trade upsert flip ("JJNS JFJ    ";"," )0:x}]`:fifo;
hdel `:fifo;
trade:update `g#sym from select from trade where msgType=220;
```

In the above:
- Linux fifo is used to efficiently parse the gzipped data
- The function `.Q.fps` is used to read files too large to fit into memory, it loops over a file in conveniently-sized chunks of complete records, and applies a function to each chunk
- The schema `"JJNS JFJ    "` specifies the datatypes for each column
- The `","` ensures the CSV is parsed using commas as delimiters.

Now the dataset has been loaded, we can inspect the table using `first` to see what the first row of the table looks like:
```q
first trade
```
    msgType   | 220
    sequenceNo| 58765
    time      | 0D07:00:00.105862814
    sym       | `TMF
    tradeId   | 24476
    price     | 4.74
    volume    | 4000

This shows a single trade message for the symbol TMF executed at a price of $4.74 for 4,000 shares at 7:00:00 on October 2, 2023.

Now that we understand the structure of the data, we can move on to efficiently querying using TSS.

## 3. Performing TSS Searches

Now, we create a [random walk](#https://en.wikipedia.org/wiki/Random_walk) float vector which we will use as our pattern we wish to find matches of. We choose a pattern length of 64 arbitrarily here, feel free to experiment with your own query vectors.

```q
q:10*abs sums neg[0.5]+64?1f
```
In the above:
- `64?1f` generates 64 random float numbers between 0 and 1 
- `neg[0.5]` returns -0.5
- `sums` computes the cumulative sum, `abs` tables the absolute value of these and finally we multiply by `10*`

The result is a random pattern that fluctuates around zero, mimicking price fluctuations that we may want to detect in the time-series data.

### Example Query: Simple Full Query on Price

We can run a simple tss search across our price column using the `.ai.tss.tss` function:
```q
select .ai.tss.tss[price;q;5;::] from trade
```
    x               
    --------------------------------------------
    4.183493 4.325539 4.428894 4.444777 4.455221
    3344263  717111   3516628  717110   1561041 

The result is a 2 element tuple of distances and ids:
- The first value is the Euclidean (L2) distance between the query `q` and the matched pattern.
- The second value is the position in the time series where the matched pattern begins.

To also return the matching pattern itself, we can pass the optional parameter `returnMatches` set to:

```q
select flip .ai.tss.tss[price;q;5;enlist[`returnMatches]!enlist 1b] from trade
```
    x                                              ..
    -----------------------------------------------..
    4.183493 3344263 15.59 12.745 15.59 106.55 9.38..
    4.325539 717111  8.22 9.48 53.45 53.45 86.57 86..
    4.428894 3516628 95.27 95.27 95.27 176.08 69.56..
    4.444777 717110  111.39 8.22 9.48 53.45 53.45 8..
    4.455221 1561041 52.67 52.68 52.68 52.68 52.68 ..

We can see in the above, our 2 element tuple changes to a 3 element tuple, where the third element is the pattern we considered as a match.

Additionally, we can turn off the default [Z-normalization](#https://en.wikipedia.org/wiki/Standard_score) (which normalizes each sliding window) by setting the `normalize` flag to false. Ths may be useful depending on your dataset for example for raw, unnormalized data:

```q
select flip .ai.tss.tss[price;q;5;`normalize`returnMatches!01b] from trade
```
    x                                              ..
    -----------------------------------------------..
    48.2234  772235 2.95 2.95 2.95 2.95 2.95 2.95 2..
    51.05442 772232 3.43 8.745 3.425 2.95 2.95 2.95..
    51.38082 772234 3.425 2.95 2.95 2.95 2.95 2.95 ..
    51.61295 772236 2.95 2.95 2.95 2.95 2.95 2.95 2..
    52.63218 772231 3.43 3.43 8.745 3.425 2.95 2.95..

These results are not very informative in our current setup, as we're running the query across all symbols in the dataset. What we really want is to query by symbol—we'll cover that next.

### Example Query: Query on Price by Sym

When running a time series similarity (TSS) search grouped by `sym`, we need to account for potential errors that can occur, such as:
- The length of the query vector being larger than the length of the timeseries being searched
- The number of matches requested less than the possible number of matches for the given query and timeseries

We can handle these cases by setting the ignoreErrors flag:
```q
select .ai.tss.tss[price;q;5;enlist[`ignoreErrors]!enlist 1b] by sym from trade
```
    sym     | x                                                                     
    --------| ----------------------------------------------------------------------
    A       | (5.137218 5.22949 5.585057 5.837146 5.842795;1601 1602 1600 1453 1451)
    AA      | (5.294841 5.376293 5.395475 5.443652 5.450586;1538 3490 483 1539 1537)
    AAAU    | (10.68361 10.68665 10.77256 10.82973 11.03451;1 2 0 3 4)              
    AAC     | ()                                                                    
    AAC WS  | ()                                                                    
    AACT    | ()                                                                    
    AACT U  | ()                                                                    
    AACT WS | ()                                                                    
    AADI    | ()                                                                    
    AAIC    | (6.645147 6.896886 7.188622 7.650043 8.034796;26 25 24 23 22)         
    AAIC PRB| ()                                                                    
    AAIC PRC| ()                                                                    
    AAIN    | ()                                                                    
    AAL     | (5.684864 5.733719 5.958707 6.023001 6.038795;56 55 54 1158 1159)     
    AAN     | (5.364869 5.466002 5.471068 6.067484 6.130456;388 389 387 390 208)    

Symbols with insufficient data return empty results (), while others return a list of tuples. 

We can also include the optional parameters we introduced earlier— `returnMatches` and `normalize` —along with `ignoreErrors`:

```q
select .ai.tss.tss[price;q;10;`normalize`returnMatches`ignoreErrors!111b] by sym from trade
```
    sym     | x                                    ..
    --------| -------------------------------------..
    A       | (5.137218 5.22949 5.585057 5.837146 5..
    AA      | (5.294841 5.376293 5.395475 5.443652 ..
    AAAU    | (10.68361 10.68665 10.77256 10.82973 ..
    AAC     | ()                                   ..
    AAC WS  | ()                                   ..
    AACT    | ()                                   ..
    AACT U  | ()                                   ..
    AACT WS | ()                                   ..
    AADI    | ()                                   ..
    AAIC    | (6.645147 6.896886 7.188622 7.650043 ..
    AAIC PRB| ()                                   ..
    AAIC PRC| ()                                   ..
    AAIN    | ()                                   ..
    AAL     | (5.684864 5.733719 5.958707 6.023001 ..
    AAN     | (5.364869 5.466002 5.471068 6.067484 ..

By default, KDB-X does not parallelize operations inside a by clause. Let's take a look at how we can do this in the next section.

### Example Query: Query on Price by Sym Parallelised

To enable the parallel execution of a function on multiple arguments we can use the[`peach`](#https://code.kx.com/q/basics/peach/) function, it's name is derived from the combination of “parallel” and “each”. 

We parallelise the above two examples by performing a `peach` over our unique symbols:

```q
s!{exec .ai.tss.tss[price;q;5;enlist[`ignoreErrors]!enlist 1b] from trade where sym=x} peach s:asc distinct trade`sym
```
    A       | (5.137218 5.22949 5.585057 5.837146 5..
    AA      | (5.294841 5.376293 5.395475 5.443652 ..
    AAAU    | (10.68361 10.68665 10.77256 10.82973 ..
    AAC     | ()
    AAC WS  | ()
    AACT    | ()
    AACT U  | ()
    AACT WS | ()
    AADI    | ()
    AAIC    | (6.645147 6.896886 7.188622 7.650043 ..
    AAIC PRB| ()
    AAIC PRC| ()
    AAIN    | ()
    AAL     | (5.684864 5.733719 5.958707 6.023001 ..
    AAN     | (5.364869 5.466002 5.471068 6.067484 ..

In the above:
- `exec` used to return results as a dictionary (before we used `select` to return as table)
- `asc distinct` gets the list of unique symbols in ascending order 

The results are functionally identical to the grouped by sym query, but computed in parallel for improved performance. You can apply this same approach to parallelize the other examples (e.g., with returnMatches, normalize, etc.).

We can measure the speed difference using the `\t` (timer) command:

```q
\t select .ai.tss.tss[price;q;5;enlist[`ignoreErrors]!enlist 1b] by sym from trade
```
    1237
```q
\t s!{exec .ai.tss.tss[price;q;5;enlist[`ignoreErrors]!enlist 1b] from trade where sym=x} peach s:asc distinct trade`sym
```
    482

These results above came from running on 4 secondary threads, results will vary depending on your specific secondaries and dataset. You can adjust the number based on your system’s CPU cores.

> Note: To take advantage of parallel execution, you must start your process with secondary threads enabled. You can do this using the -s command-line flag when launching the process e.g. `q -s 4`


## Conclusion

This tutorial demonstrated how to:
- Load data from gzipped files using named pipes
- Search on timeseries data for specified patterns of interest
- Conduct efficient TSS searches utilising Q's parallel functionalities 
