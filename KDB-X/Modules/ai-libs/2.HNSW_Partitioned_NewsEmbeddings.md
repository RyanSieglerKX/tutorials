# HNSW Partitioning with Hacker News Embeddings
> **Note**: This tutorial is part of a beta release of software that is not yet publicly available. Please contact KX for access.
> 
This tutorial walks through the process of creating a historical database (HDB) filled with Hacker News data and embeddings. It then demonstrates how to build an HNSW (Hierarchical Navigable Small World) index per partition for efficient nearest neighbor searches.

## 1. Prerequisites

1. Requires KDB-X to be installed, you can follow the installation guide at https://kx.com/products.
2. Ensure you have the necessary dataset:
   1. Download the dataset from Kaggle: [Hacker News OpenAI Embeddings](https://www.kaggle.com/datasets/julien040/hacker-news-openai-embeddings?resource=download)
   2. Extract `archive.zip` into the same directory where you are running this from. The file `story.csv` should be available after unzipping.

## 2. Loading and Preparing the Data

Launch a q session and load the ai-libs initialization script:
```q
\l ai-libs/init.q
```

Load the dataset (`story.csv`) into a table:

```q
tab:("JSSJJSS*";enlist",") 0: `:story.csv
```
In the above:
- The schema `"JSSJJSS*"` specifies the datatypes for each column
- The `enlist","` ensures the CSV is parsed using commas as delimiters.

Then Convert UNIX timestamps to KDB-X timestamps and Filter the data for a specific date range (2022-01-01 to 2023-01-01):
```q
tab:update time:("p"$(time*1e9)-946684800000000000) from tab;
tab:select from tab where time.date within (2022.01.01;2023.01.01)
```
In the above:
- The subtraction of 946684800000000000 aligns the timestamp to the KDB-X epoch (2000-01-01)
- `time.date` extracts the date portion from the timestamp

Finally, parse the embeddings column and Extract distinct dates from the dataset:
```q
tab:update embeddings:0^flip (1536#"E";",") 0: embeddings from tab;
dts:asc exec distinct `date$time from tab;
```
In the above:
- `1536#"E"` ensures each embedding vector has 1536 dimensions
- `flip (...) 0:` embeddings transforms the string data into a matrix format
- `0^` replaces any missing values with 0
- `exec` extracts list of dates which we can use for partitioning the dataset by date later

Now the dataset has been loaded, we can inspect the table using `first` to see what the first row of the table looks like:
```q
first tab
```
    id        | 31102089
    title     | `Memray: a memory profiler for Python
    url       | `https://github.com/bloomberg/memray
    score     | 495
    time      | 2022.04.20D19:54:19.000000000
    comments  | `48
    author    | `shcheklein
    embeddings| 0 -0.01145057 0.003559053 -0.05300251 -0.0181101 0.007672496 0.0146058 -0.0293485 -0.005352268 -0.01444154 -0.006354962 0.01051289 -0.004222953 -0.01731616 -0.01463318 -0.01325063 -0.005109..

From the first row, we can see that the dataset contains key information about Hacker News stories. The embeddings column consists of a 1536-dimensional numerical representation of the story, which will be useful for similarity searches.

Now that we understand the structure of the data, we can move on to processing and indexing it for efficient querying.

## 3. Create HNSW Index

Now, we create an HNSW index per partition. To enable fast similarity searches, we create an HNSW (Hierarchical Navigable Small World) index for each partitioned date. HNSW is an efficient nearest-neighbor search algorithm that allows us to quickly retrieve similar embeddings from large datasets.

The following code iterates over each unique date in the dataset (dts), creating an HNSW index and saving both the index and the corresponding news data into a partitioned database.
```q
{[dt;t]
    hnswobj: .ai.hnsw.put[();(); exec embeddings from t where dt=`date$time; `L2; 8; 0Ne; 8];
    hnsw: ([] hnsw: enlist hnswobj);
    (hsym `$"db/", string[dt], "/hnsw/") set hnsw;
    (hsym `$"db/", string[dt], "/news/") set .Q.en[`:db] `time xasc select from t where dt=`date$time;
    .Q.gc[];
 }[;tab] each dts;
```
In the above:
- `.ai.hnsw.put` builds an HNSW index for embeddings corresponding to the specific date (dt)
- `enlist` lets us store the HNSW index in a structured table
- `set` saves the HNSW index and news data to partitioned table on disk
- Running `.Q.gc[]` after processing each partition reduces memory usage
- The function is applied (`each dts`) across all unique dates, processing them one by one
  
We can verify database integrity with `.Q.chk`:

```q
.Q.chk[`:db]
```

And load the database:

```q
.Q.lo[`:db;0;0];
```

## 4: Querying the HNSW Index
Now that we have built and stored the HNSW index, we can use it to efficiently search for similar news articles based on their embeddings. This section demonstrates how to query the index to find similar stories and refine results using additional filters.

### Querying with HNSW
We define a new function `.walkthroughUtil.queryPartitionHnsw` which will perform a similarity search across multiple date partitions using the HNSW index.
```q
.walkthroughUtil.queryPartitionHnsw:{[q;sd;ed;wc;k;metric;efs]    
    filterSearch:$[wc~();0b;1b];
    wc:enlist[(within;`date;(enlist;sd;ed))],wc;
    res:?[news;wc;0b;(`incIds`date)!(`i`date)];
    dts:exec distinct date from res;
    order:(idesc;iasc)@(metric~`L2);
    tab:raze {[t;q;k;metric;efs;fs;order;dt]
        res:select from news where date = dt;
        s:$[fs;
            .ai.hnsw.filterSearch[res`embeddings;(first (select from hnsw where date=dt)`hnsw);q;k;metric;efs;exec incIds from t where date = dt];
            .ai.hnsw.search[res`embeddings;(first (select from hnsw where date=dt)`hnsw);q;k;metric;efs]];
        s@\:order[s[0]];
        delete incIds from `date`id`dist xcols (res@s[1]),'([]dist:s[0])
        }[res;q;k;metric;efs;filterSearch;order;]peach dts;
    tab:tab@k sublist order tab`dist
 }
```
In the above:
- The function first filters news by date (`sd` to `ed`) and applies any additional where conditions (`wc`)
- It finds the unique dates from the filtered results.
- For each date, it selects news articles, retrieves the HNSW index, and performs an approximate nearest neighbor search using `.ai.hnsw.filterSearch`
- The results are ordered based on the similarity metric (L2 in this case)
- The function retrieves the top k matches per partition and merges them into a single output table



### Example Query: Finding Similar News Articles

To find similar stories, we first select an interesting title from our dataset.
```q
select title, score from news where date within (2022.01.01; 2023.01.01), id=29755242
```
    title                                          score
    ----------------------------------------------------
    Finding your home in game graphics programming 247

Retrieve the embedding for this article:

```q
q: first exec embeddings from select embeddings from news where date within (2022.01.01; 2023.01.01), id=29755242
```

Run a nearest neighbor search using HNSW to get the top 5 most similar articles:

```q
t: .walkthroughUtil.queryPartitionHnsw[q; 2022.01.01; 2023.01.01; (); 5; `L2; 8];
select dist, title, score from t
```
    dist      title                                                     score
    -------------------------------------------------------------------------
    0         Finding your home in game graphics programming            247  
    0.2746519 Why are 2D vector graphics so much harder than 3D? (2019) 135  
    0.3037856 The harsh truth of video games programming                175  
    0.3080716 A Great Old-Timey Game-Programming Hack (2013)            140  
    0.3113103 The Apple GPU and the impossible bug                      965  

These results show articles that are semantically similar to the original query.

We can further refine the results by filtering for highly scored stories. First, we check the score distribution:
```q
select max score, min score, avg score from news
```
    score score1 score2
    ---------------------
    4298  100    251.2842

Run a filtered search for high-score articles:

```q
t: .walkthroughUtil.queryPartitionHnsw[q; 2022.01.01; 2023.01.01; enlist (<;500;`score); 5; `L2; 8];
select dist, title, score from t
```
    dist      title                                                                           score
    -----------------------------------------------------------------------------------------------
    0.3113103 The Apple GPU and the impossible bug                                            965  
    0.3219757 Aging programmer                                                                596  
    0.3530114 Iâ€™m a productive programmer with a memory of a fruit fly                      650  
    0.3765028 Electrical engineers on the brink of extinction threaten entire tech ecosystems 526  
    0.3849241 Tales of the M1 GPU                                                             1014 

The score filter allows us to refine search results by excluding articles with lower scores. This helps prioritize more popular or impactful articles while still maintaining relevance based on embedding similarity.

## 5. Querying via Python
You can query your database using KDB-X Python. This allows Python users to interact with the database without needing to work directly in KDB-X.

In your Python code, `import pykx` and load the necessary KDB-X/q scripts. You should move the `.walkthroughUtil.queryPartitionHnsw` to a script called `utils.q` for the below:
```bash
pip install --pre pykx
```
```python
import pykx as kx
import numpy as np
kx.q('\l ai-libs/init.q')
kx.q('.Q.lo[`:db;0;0]')
kx.q('\l utils.q')
```

Call the  `kx.q.walkthroughUtil.queryPartitionHnsw` function from Python like this:
```python
kx.q.walkthroughUtil.queryPartitionHnsw(
    np.random.rand(1536).astype(np.float32),  # random query vector
    np.datetime64('2022-01-01'),
    np.datetime64('2023-01-01'),
    [],  # Additional functional where clause
    5,   # Number of results
    "L2", # Distance metric
    8    # Neighbors
)
```

This lets Python users easily query KDB-X and get results directly in their Python workflow.

## Conclusion

This guide demonstrated how to:
- Load Hacker News data into KDB-X
- Create an HDB with partitions
- Generate HNSW indexes for fast nearest-neighbor searches
- Query for similar articles using HNSW
- Filter search results by specific conditions
- Query using KDB-X Python
